(library (yuni ffi ctemplate prologue)
         (export
           put-c-prologue)
         (import (yuni scheme)
                 (yuni base match)
                 (yuni ffi ctemplate util)
                 (yuni ffi database root)
                 (yuni ffi database prologue))

;; c-template: prologue

(define (put-c-prologue port db)
  (define prologue (prologue-source (database-prologue db)))
  (define (p . obj) (apply put-obj port (append obj (list "\n"))))
  (define (one e)
    (match e
           (('cpp-define sym/str)
            (p "#define "
               sym/str))
           (('cpp-import-predefine sym)
            'do-nothing)
           (('cpp-if str then)
            (p "#if " str)
            (one then)
            (p "#endif"))
           (('cpp-if str then else)
            (p "#if " str)
            (one then)
            (p "#else")
            (one else)
            (p "#endif"))
           (('cpp-begin . forms)
            (for-each one forms))
           (('cpp-ifdef str then)
            (p "#ifdef " str)
            (one then)
            (p "#endif"))
           (('cpp-ifdef str then else)
            (p "#ifdef " str)
            (one then)
            (p "#else")
            (one else)
            (p "#endif"))
           (('cpp-include file)
            (p "#include \"" file "\""))
           (('cpp-system-include file)
            (p "#include <" file ">"))))
  (for-each one prologue))

)
